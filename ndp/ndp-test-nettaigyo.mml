#TITLE "淋しい熱帯魚 PSG Cover"
#MEMO "Wink 1989年7月5日 作詞：及川眠子"
#COMPOSER "	尾関昌也"
#ARRANGER "	船山基紀"

#BSAVE-NAME "SAM-ISII"
#PROGRAMMER "Uniskie"

;#TEMPO 128
#TIME-BASE 28 ;ゲートタイムに端数を出さずにテンポ指定したい場合こちら
#REPEAT-MODE 1 ;チェック付きリピートブロック圧縮

;音色定義
@R:B = *3 :940 V15 / *1 :1680 V13 /  :2200 V12
@R:S = *3 :520 N2 V15 / *2 N1 V14 / *1 :670 V13 / :820 V12 / :970 v11
@R:H = *3 :20 N0 V12
@R:C = *3 :21 N1 V12 / V11 / v10
@R:K = *3 :450 N8  V15 / *1 :500 V14 / :550 V13 / :650 V12
@R:M = *3 :500 N10 V15 / *1 :630 V14 / :760 V13 / :860 V12
@R:N = *3 :720 N12 V15 / *1 :870 V14 / :1020 V13 / :1220 V12

@0=DFE:2 D:3 C:4 B:6 L A:4 B:4
@1=CEFE:2 D:3 C:4 B:6 L A:4 B:4
@2=FD:2 C:3 B:4 A:5 9:6 8:7 7:8 6:8 5:8 4:8 3:8 2:8 1:8 L0
@6=FD L B:4 A:4 9:4
@7=A:6 B:6 C:5 D:5 E:4 FE:4 D:5 C:6 LB

;ビブラート
@m0=42,3,3,8

;ノートエンベロープ
@n9=0 -1 -2 -3 -4 -5 -6 -7 -8 -9 -10 -11 -12
@n1=-4 0 

; ***** intro *****

123 o5v13
r   v15 @c 3

123  @0 @q1 @r11,12 
123  @rd1,12 @m0

123 v-2
1  l16 aggrg8.r
2  l16 eddrd8.r
3  l16 c<bbrb8.>r
r  r2

1  a8r8a8ra8ggrg8.r a8a8a8aa8ggrg8.r
2  e8r8e8re8ddrd8.r e8e8e8ee8ddrd8.r
3  c8r8c8rc8<bbrb8.>r c8c8c8cc8<bbrb8.>r
r  r1 r1

1  @1 
23 \-1
1  [q5<a8>e8d8e8<a8>q8edrder]4
2  [a8r8a8ra8ggrg8.r a8a8a8aa8ggrg8.r]2
3  [c8r8c8rc8<bbrb8.>r c8c8c8c/c8<bbrb8.>r]2
3                              c
3                  v+2@2@n9 <<g8g8e16c8.>> @n-@1v-2
r  b4b4b4b4   b4b4b4b8h16h16
r  b4b8h8b4b8h8 b8h8b4k8k8m16n8.

23 \0
12 @7v-1
1  >a2.<r8.<a> d2.r4@1
2   d2. r4     e2;^8r4.
12 v+1
2                    v+2@2@n9 <e16e16e8e8e16e16> @n-@1v-2
3  <a4v-2a>de<a8a>de<a8r8 v-2a>de<a8v-2a>de<a8v-2a>de<a8rv+8
r  b4b4b8h16h16b8h8 b16h16h8b4s16s16s8s8s16s16

; ***** loop *****

1  v+2
2  v-1
3  v+4 <<

;*****************************************
; NDP 0.9.79の仕様変更：
;*****************************************
; @r リリースの動作:
;
; ノーマルトラックでsコマンドを使用したH.ENVでもリリースが有効になった。
; 必要に応じて@r-で解除する必要がある。
;
; H.ENVの場合、
; ソフトウェアエンベロープの掛かっていないTONEでリリースが発音される。
;
; ※ リリースにソフトウェアエンベロープをかけたい場合、セルフディレイを使用する

;3  @6q4 @r11,9 @rd- @m- ;矩形波ベース
3  s14,3 @q1 @r- @rd- @m- ;H.ENV 三角波ベース

;*****************************************
; * NDPBIN_20250301.zip 11:45版 動作検証
;*****************************************
/*
;H.ENV使用時のリリースディレイバグ
1 @r10,6  ; H.ENVでリリース有効にしている場合
1 @rd1,12 ; これと
1 @m0     ; これの、両方使っていると
          ; 休符でもH.ENVが発音する様になる
          ; （どちらかコメントアウトすると休符で鳴らない）
          ; ※ おそらく以前からあるもの
1 s8 m+ mk-24 \1 @q2
$mainVoice0=;空にする
$mainVoice1=;空にする
$mainVoice2=;空にする
*/

$mainVoice0=@6
$mainVoice1=@1
$mainVoice2=@0
$subVoice0=@2
$subVoice1=@1

123r @l

1 j1
1 $mainVoice0:
1 l8 [[<a>eded.c16/r16c16d16c16] r4
1      <a>eded.c16<r16a16b16>c16 fgecd.e16r4]
2 $subVoice0 \-1
2 @n1
2 l16
2 [a8.rr2raaa a8.rr2ra8a
2  a8.rr2raaa [a8.rr4]]
2 @n-
3 l8
3 [[a>a<]4 [g>g<]4 [f>f<]4 [d>d<]2[e>e<]2]
r [b4m4/b4m4]8 b8m8.m16m16m16

1 j2
1 $mainVoice1:
1 e4      v-2\2d+16&e8.\0v+2reed& dcce-16&e16&ed4c& c4c<a>cd4e& e4.f16e16d4rd8
1 d+16&e8.v-2\2d+16&e8.\0v+2reed& dg4a4g4r d4ded4<gb& b>c16<b16a4.>r4.
2 $subVoice1: \-1
2 l16 
2 [q5<a8>e8d8e8<a8>q8edrder]7
2  q5<a8>e8d8c8<a8>q8edrder
3 [[a>a<]4[g>g<]4 / [f>f<]4[g>g<]4]
3                   [d>d<]2 [e>e<]2 [a>a<]4
r [[b4m4]6 / b4m4b4m8h8b8h8m4]
r            b8b16b16m16m16n16n16

1 [r4agr4ag rabe4g4./r4agab4e& eg4ed4r4]
1                    r4agab4b& b2e2
2 l8
2 r4   v+3edr4ed r4.v-3e<gab>c< a4r2.    r4.>e<b>c<bg>
2 e.r16v+3edr4ed r4.v-3e<gab>c e.<r16r2. r2v+1g+l16eedeg8>v-1
3 [[f>f<]4 [g>g<]4 [f>f<]4 / [e>e<]4 ]2
3                            [g>g<]2 [e>e<]2
r [[b4m4]3 b8b8m16m16n16n16]3
r [b4m4] b8m8v-m4v+b4m16m16n16n16

1 j3
1 aa4a&a2 r1 gg4a4g4. r1
1 f4fcfg4g& g2g+2
2 [l8v-1q5aece<a>q8l16edr8v+>cd eredrdcrl8<aagr]2
2 @n1[rf]4@n- rede<b>l16edrderl8
3 [a>a<]8 [g>g<]8 
3 [f>f<]4 [g>g<]2[a->a-<]2
r [[b4m4]3 b8m8/m4] m8b8
r [b4m4]3 b8m8m16m16n16n16

1 j4
1 aaaa&a2 gg4a4g4. f4faggga& a4r4a4>c4
2 l16[r2r8edrder]2 l8q5<f>ede<e>q8l16dcrcdr l8q5redeq8r<a>rc
3 [a>a<]4 [g>g<]4  [d>d<]2 [e>e<]2 [a>a<]4
r [b4m4]3 b4m8m16m16
r b8b8m4 [b4m4]2 b4m8b8

1 j5
1 [dddcd4e4]
1  dddcdc4c& c1<
2 $mainVoice1: \0
2 [fffef4g4]
2  fffefe4e l16ec<ae >c<aec aec<a >ec<afl8
	; ここのベースの耳コピ―は大分怪しい
3 [f>f<]2[g>g<]2 [d>d<]2[e>e<]2
3 [f>f<]2[g>g<]2 [a>a<]4
r [[b4m8h8]3 / b8h8m8b8] b4k16m16n8

12 $mainVoice2:
1 [a4r2.]
2 \1
2 >>[r4a4/r2]<<
2         v+3@2@n9 g16g16e8g16e16e16e16 @n-v-3
3 [a>a<]8
r [b4m4]3 k16k16m8k16m16m16m16

23 @rd-1
