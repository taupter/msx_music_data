10 CLEAR 200,&H9000:DEFINT A-Z
20 AD=&H9000 'LOAD ADR
30 SCREEN 0:WIDTH 40:COLOR 15,4,7
40 PRINT"LOAD NDP.BIN":GOSUB 210
50 '
60 RESTORE
70 '
80 READ F$:IF F$="*" THEN 60
90 PRINT:F$=F$+".NDP":PRINT"PLAY:"+F$
100 BLOAD F$,AD
110 A=USR2(AD) 'SET ADR
120 A=USR(0) 'PLAY
130 READ T$:PRINT T$
140 '
150 TR=STRIG(1) OR STRIG(2) OR (INKEY$<>"")
160 IF TR=0 THEN 150
170 A=USR7(5) ' FADEOUT
180 TIME=0:FOR I=0 TO 5*16:I=TIME:NEXT
190 GOTO 80
200 '
210 'SET UP NDP 
220 BLOAD"NDP.BIN"
230 DEFUSR=&HC003  'PLAY
240 DEFUSR1=&HC006 'STOP
250 DEFUSR2=&HC00C 'SET BGM ADR
260 DEFUSR3=&HC00F 'MUTE PSG A(2-255)
270 DEFUSR4=&HC012 'MUTE PSG B
280 DEFUSR5=&HC015 'MUTE PSG C
290 DEFUSR6=&HC018 'VOLUME REDUCE(0-15)
300 DEFUSR7=&HC01B 'FADE OUT(1-255)
310 ON STOP GOSUB 350:STOP ON
320 ON ERROR GOTO 370
330 RETURN
340 ' ON STOP
350 A=USR7(5):TIME=0:FOR I=0 TO 5*16:I=TIME:NEXT:A=USR1(0):PRINT"STOP":END
360 ' ON ERROR
370 A=USR1(0):ON ERROR GOTO 0:RESUME
380 '
390 DATA DRSP-ZAW,"Dragon Spirit -ZAWEL-"
430 DATA DRSP-OMK,"Dragon Spirit -OMAKE-"
440 DATA F1SP-1,"F1-SPIRIT (H.ENV): F1 Shuffle"
450 DATA F1SP-2,"F1-SPIRIT (H.ENV): Starting Grid"
460 DATA F1SP-3,"F1-SPIRIT (H.ENV): D/G"
470 DATA F1SP-4,"F1-SPIRIT (H.ENV): Hot Summer Riding (STOCK)"
480 DATA F1SP-5,"F1-SPIRIT (H.ENV): Random Approach (RALLY)"
490 DATA F1SP-6,"F1-SPIRIT (H.ENV): Next (RETIRE)"
500 DATA F1SP_1,"F1-SPIRIT (Standard): F1 Shuffle"
510 DATA F1SP_2,"F1-SPIRIT (Standard): Starting Grid"
520 DATA F1SP_3,"F1-SPIRIT (Standard): D/G"
530 DATA F1SP_4,"F1-SPIRIT (Standard): Hot Summer Riding (STOCK)"
540 DATA F1SP_5,"F1-SPIRIT (Standard): Random Approach (RALLY)"
550 DATA F1SP_6,"F1-SPIRIT (Standard): Next (REITRE)"
560 DATA SOMD1-01,"SORCERIAN MD: THE DARKEST OF THE DARK - Dungeon (H.ENV)"
570 DATA SOMD1_01,"SORCERIAN MD: THE DARKEST OF THE DARK - Dungeon (Standard)"
580 DATA DDS2LABY,"DDS2: Labyrinth - at 2am"
590 DATA DDS2OMEG,"DDS2: OMEGA - Jihad"
600 DATA GALIO-02,"MAZE of GALIOUS: CASTLE"
610 DATA GALIOH02,"MAZE of GALIOUS (H.ENV): CASTLE"
620 DATA SALA_M01,"SALAMANDER (H.ENV): POWER OF ANGER"
630 DATA SALA_M02,"SALAMANDER (H.ENV): FLY HIGH"
640 DATA SALA_P01,"SALAMANDER: POWER OF ANGER"
650 DATA SALA_P02,"SALAMANDER: FLY HIGH"
660 DATA SALA_O01,"SALAMANDER (H.ENV): POWER OF ANGER (HANA-UTA)"
670 DATA DSEED-1,"DANGEROUS SEED: 1st Tube"
680 DATA DSEED-1B,"DANGEROUS SEED: Triple-Eye"
690 DATA DSEED-2,"DANGEROUS SEED: 2nd Tube"
700 DATA DSEED-5,"DANGEROUS SEED: Jupiter"
710 DATA GRAII-7,"GRADIUS II AC: Synthetic Life"
720 DATA GRAII-16,"GRADIUS II AC: Shoot & Shoot"
730 DATA PAROCRIS,"PARODIUS: Crisis 4th movement"
740 DATA VANILLA,"VANILLA DE KOUSHUUNYU"
750 DATA *
