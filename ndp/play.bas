10 CLEAR 200,&HAC00:DEFINT A-Z
20 AD=&HAC00 'LOAD ADR
30 SCREEN 1:WIDTH 32:COLOR 15,4,7
40 PRINT"LOAD NDP.BIN":GOSUB 200
50 '
60 RESTORE
70 '
80 READ F$:IF F$="*" THEN 60
90 PRINT"PLAY:"+F$
100 BLOAD F$+".NDP",AD
110 A=USR2(AD) 'SET ADR
120 A=USR(0) 'PLAY
130 '
140 TR=STRIG(1) OR STRIG(2) OR (INKEY$<>"")
150 IF TR=0 THEN 140
160 TIME=0 ' FADEOUT
170 I=TIME\4:IF I<15 THEN A=USR7(I):GOTO 170
180 GOTO 80
190 '
200 'SET UP NDP 
210 BLOAD"NDP.BIN"
220 DEFUSR=&HC003  'PLAY
230 DEFUSR1=&HC006 'STOP
240 DEFUSR2=&HC00C 'SET BGM ADR
250 DEFUSR3=&HC00F 'MUTE PSG A(2-255)
260 DEFUSR4=&HC012 'MUTE PSG B
270 DEFUSR5=&HC015 'MUTE PSG C
280 DEFUSR6=&HC018 'VOLUME REDUCE(0-15)
290 DEFUSR7=&HC01B 'FADE OUT(1-255)
300 ON STOP GOSUB 340:STOP ON
310 ON ERROR GOTO 360
320 RETURN
330 ' ON STOP
340 A=USR1(0):PRINT"STOP":END
350 ' ON ERROR
360 A=USR1(0):ON ERROR GOTO 0:RESUME
370 '
380 DATA DSEED-1,DSEED-1B,DSEED-2,DSEED-5,F1SPTITL
390 DATA GRAII-7,GRAII-16,PAROCRIS,VANILLA,*
