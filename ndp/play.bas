10 CLEAR 200,&H9000:DEFINT A-Z
20 AD=&H9000 'LOAD ADR
30 SCREEN 0:WIDTH 40:COLOR 15,4,7
35 PRINT"[L]:AUTOPLAY"
40 PRINT"LOAD NDP.BIN":GOSUB 350
50 '
60 RESTORE
70 '
80 ' LOAD NDP MUSIC DATA
90 READ F$:IF F$="*" THEN 60
100 PRINT:F$=F$+".NDP":PRINT"PLAY:"+F$
110 BLOAD F$,AD
120 A=USR2(AD) 'SET ADR
130 A=USR(0) 'PLAY
135 READ NL '1=NO LOOP
140 READ T$:PRINT T$
150 '
160 ' MAIN LOOP
170 I$=INKEY$
180 IF (I$="L") OR (I$="l") OR STRIG(3) OR STRIG(4) THEN 290
190 TR=STRIG(1) OR STRIG(2) OR (I$<>"")
200 IF LC THEN TR=TR OR (USR8(LA)>=(LC-NL))
210 IF TR=0 THEN 170
220 '
230 ' PLAY NEXT
240 A=USR7(5) ' FADEOUT
250 TIME=0:FOR I=0 TO 5*16:I=TIME:NEXT
260 GOTO 90
270 '
280 ' CHANGER AUTOPLAY MODE
290 LC=2-LC ' LOOP 2 <-> 0
300 IF LC THEN PRINT"*AUTOPLAY LOOP="+HEX$(LC)
310 IF LC=0 THEN PRINT"*AUTOPLAY OFF"
320 I$=INKEY$:IF (I$="L") OR (I$="l") OR STRIG(3) OR STRIG(4) THEN 320
330 GOTO 170
340 '
350 'SET UP NDP 
360 BLOAD"NDP.BIN"
370 DEFUSR=&HC003  'PLAY
380 DEFUSR1=&HC006 'STOP
390 DEFUSR2=&HC00C 'SET BGM ADR
400 DEFUSR3=&HC00F 'MUTE PSG A(2-255)
410 DEFUSR4=&HC012 'MUTE PSG B
420 DEFUSR5=&HC015 'MUTE PSG C
430 DEFUSR6=&HC018 'VOLUME REDUCE(0-15)
440 DEFUSR7=&HC01B 'FADE OUT(1-255)
450 DEFUSR8=&HF975:BLOAD"USRGETA.BIN" '! USE VOICAQ !
460 'USRGETA : A=USR(ADR) -> CALL(ADR) & ret A
470 LA=&HC036 'RDLOOP
480 SA=&HC030 'RDSTAT
490 LC=0 'AUTO PLAY LOOP COUNT
500 ON STOP GOSUB 540:STOP ON
510 ON ERROR GOTO 560
520 RETURN
530 ' ON STOP
540 A=USR7(5):TIME=0:FOR I=0 TO 5*16:I=TIME:NEXT:A=USR1(0):PRINT"STOP":END
550 ' ON ERROR
560 A=USR1(0):ON ERROR GOTO 0:RESUME
570 '
580 DATA DRSP-SEA,0,"Dragon Spirit -SEA-"
590 DATA DRSP-AR1,0,"Dragon Spirit AREA1 -The Paleozoic Era-"
600 DATA DRSP-ZAW,0,"Dragon Spirit -ZAWEL-"
610 DATA DRSP-OMK,0,"Dragon Spirit -OMAKE-"
620 DATA F1SP-1,1,"F1-SPIRIT (H.ENV): F1 Shuffle"
630 DATA F1SP-2,1,"F1-SPIRIT (H.ENV): Starting Grid"
640 DATA F1SP-3,0,"F1-SPIRIT (H.ENV): D/G"
650 DATA F1SP-4,0,"F1-SPIRIT (H.ENV): Hot Summer Riding (STOCK)"
660 DATA F1SP-5,0,"F1-SPIRIT (H.ENV): Random Approach (RALLY)"
670 DATA F1SP-6,0,"F1-SPIRIT (H.ENV): Next (RETIRE)"
680 DATA F1SP_1,1,"F1-SPIRIT (Standard): F1 Shuffle"
690 DATA F1SP_2,1,"F1-SPIRIT (Standard): Starting Grid"
700 DATA F1SP_3,0,"F1-SPIRIT (Standard): D/G"
710 DATA F1SP_4,0,"F1-SPIRIT (Standard): Hot Summer Riding (STOCK)"
720 DATA F1SP_5,0,"F1-SPIRIT (Standard): Random Approach (RALLY)"
730 DATA F1SP_6,0,"F1-SPIRIT (Standard): Next (REITRE)"
740 DATA SOMD1-01,0,"SORCERIAN MD: THE DARKEST OF THE DARK - Dungeon (H.ENV)"
750 DATA SOMD1_01,0,"SORCERIAN MD: THE DARKEST OF THE DARK - Dungeon (Standard)"
760 DATA DDS2LABY,0,"DDS2: Labyrinth - at 2am"
770 DATA DDS2OMEG,0,"DDS2: OMEGA - Jihad"
780 DATA GALIO-02,0,"MAZE of GALIOUS: CASTLE"
790 DATA GALIOH02,0,"MAZE of GALIOUS (H.ENV): CASTLE"
800 DATA SALA_M01,0,"SALAMANDER (H.ENV): POWER OF ANGER"
810 DATA SALA_M02,0,"SALAMANDER (H.ENV): FLY HIGH"
820 DATA SALA_P01,0,"SALAMANDER: POWER OF ANGER"
830 DATA SALA_P02,0,"SALAMANDER: FLY HIGH"
840 DATA SALA_O01,0,"SALAMANDER (H.ENV): POWER OF ANGER (HANA-UTA)"
850 DATA DSEED-1,0,0,"DANGEROUS SEED: 1st Tube"
860 DATA DSEED-1B,0,"DANGEROUS SEED: Triple-Eye"
870 DATA DSEED-2,0,"DANGEROUS SEED: 2nd Tube"
880 DATA DSEED-5,0,"DANGEROUS SEED: Jupiter"
890 DATA GRAII-7,0,"GRADIUS II AC: Synthetic Life"
900 DATA GRAII-16,0,"GRADIUS II AC: Shoot & Shoot"
910 DATA PAROCRIS,0,"PARODIUS: Crisis 4th movement"
920 DATA VANILLA,0,"VANILLA DE KOUSHUUNYU"
930 DATA *
