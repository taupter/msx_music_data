10 CLEAR 200,&H9000:DEFINT A-Z
20 AD=&H9000 'LOAD ADR
30 SCREEN 0:WIDTH 40:COLOR 15,4,7
40 PRINT"LOAD NDP.BIN":GOSUB 210
50 '
60 RESTORE
70 '
80 READ F$:IF F$="*" THEN 60
90 PRINT:F$=F$+".NDP":PRINT"PLAY:"+F$
100 BLOAD F$,AD
110 A=USR2(AD) 'SET ADR
120 A=USR(0) 'PLAY
130 READ T$:PRINT T$
140 '
150 TR=STRIG(1) OR STRIG(2) OR (INKEY$<>"")
160 IF TR=0 THEN 150
170 A=USR7(5) ' FADEOUT
180 TIME=0:FOR I=0 TO 5*16:I=TIME:NEXT
190 GOTO 80
200 '
210 'SET UP NDP 
220 BLOAD"NDP.BIN"
230 DEFUSR=&HC003  'PLAY
240 DEFUSR1=&HC006 'STOP
250 DEFUSR2=&HC00C 'SET BGM ADR
260 DEFUSR3=&HC00F 'MUTE PSG A(2-255)
270 DEFUSR4=&HC012 'MUTE PSG B
280 DEFUSR5=&HC015 'MUTE PSG C
290 DEFUSR6=&HC018 'VOLUME REDUCE(0-15)
300 DEFUSR7=&HC01B 'FADE OUT(1-255)
310 ON STOP GOSUB 350:STOP ON
320 ON ERROR GOTO 370
330 RETURN
340 ' ON STOP
350 A=USR7(5):TIME=0:FOR I=0 TO 5*16:I=TIME:NEXT:A=USR1(0):PRINT"STOP":END
360 ' ON ERROR
370 A=USR1(0):ON ERROR GOTO 0:RESUME
380 '
390 DATA DDS2-LAB,"DDS2: Labyrinth - at 2am"
400 DATA DDS2-OMG,"DDS2: OMEGA - Jihad"
410 DATA GALIO-02,"MAZE of GALIOUS: CASTLE"
420 DATA GALIOH02,"MAZE of GALIOUS: CASTLE (H.ENV)"
430 DATA SALA_M01,"SALAMANDER: POWER OF ANGER (H.ENV)"
440 DATA SALA_P01,"SALAMANDER: POWER OF ANGER"
450 DATA SALA_O01,"SALAMANDER: POWER OF ANGER (HANAUTA)"
460 DATA F1SP-1,"F1-SPIRIT: F1 Shuffle"
470 DATA F1SP-2,"F1-SPIRIT: Starting Grid"
480 DATA F1SP-3,"F1-SPIRIT: D/G"
490 DATA F1SP-4,"F1-SPIRIT: Hor Summer Riding"
500 DATA DSEED-1,"DANGEROUS SEED: 1st Tube"
510 DATA DSEED-1B,"DANGEROUS SEED: Triple-Eye"
520 DATA DSEED-2,"DANGEROUS SEED: 2nd Tube"
530 DATA DSEED-5,"DANGEROUS SEED: Jupiter"
540 DATA GRAII-7,"GRADIUS II AC: Synthetic Life"
550 DATA GRAII-16,"GRADIUS II AC: Shoot & Shoot"
560 DATA PAROCRIS,"PARODIUS: Crisis 4th movement"
570 DATA VANILLA,"VANILLA DE KOUSHUUNYU"
580 DATA *
