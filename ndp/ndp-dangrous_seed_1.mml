;*************************************************
#title "DANGEROUS SEED(MD) 1st TUBE (STAGE 1&8)"
;*************************************************
#programmer "uniskie 2004/09/03"

; ndp.exe v0.48 / ndp.bin v0.47

#tempo 156

;********************************
; melody
;********************************

@m0 = 40,3,5,0 ,3,1
@m1 = 58,3,4,0 ,2,1 
@m2 = 28,3,3,0 ,1,1 
@0 = fededc:8 l b:1 c:1 d:1 c:1
@1 = fedc:3 edc:7 l d:2 c:1 b:2 c:1
@2 = fe d c:10 l b:3 c:3
@n0 = -1 0

1 o3 v13
1 @r13,20 @rd1,15
1 @l
1 @0 @m0
1 l8
1  [ q8 @q3
1   e1 &e4b_>c8&c8c_d8&d8e8<g8 &g1&
1   g16.>@q0f32@q2g8f8d+8c8d8d+8f16.@q0b32@q2>j4
1   c1& c8<a+8>c8<a+8f8c8c+8d+8
;  部分的に音の表情を変える
1   v-1q7f8d+8f16.@q0a32@q2a+8@n0f8d+8a8e8 f+8e8f+8>c8d8<b8a8g8<@n-v+1
1  ]2
1 @1 @m1 q8 
1 >
1  [e1& e2a4e4 g1& g2>c4<g4 >d1& d2d4e4 /
1   c+1& c+1<]2 
1 @2 @m2 q7 @q0
1   f4.f+2&f+8g4& g8g+2&g+8<
1 <

;********************************
; sequence
;********************************

@3=FEDC:2 B:2 A:2 9:3 8:3 7:4 6:4 5:4 4:4 3:4 2:5 1:5 L0

2 o4 v11 \-1
2 @r12,20 @rd1,8
2 @l 
2 @3 q4 @m-
2  l16 
2  [<aa>eeaaeeaa>eeaa>ee aaee<aaee<aaeeaaee
2   <gg>ddggddgg>ddgg>dd ggdd<ggdd<ggddggdd
2   <ff>ccffccff>ccff>cc ffcc<ffcc<ffccffcc
2   j1
2   <g+g+g+g+d+d+d+d+g+g+g+g+d+d+a+a+
2    bbbb eeee eebb>ee>cc<
2  ]2 
2  v-1
2  > q4 j2
2  [[g>c<cf]8 [g>c<cd+]8 [a>d<df+]8 / [a>c+<c+e]8
2  ]2
2  <
2 @2 @m2 v+2 q7 j3
2  a+4.b2&b8 >c4.c+2&c+8<
2  v-1

;********************************
; base
;********************************

@6 = f d:4 b:5 a:6 L 9
;@6 = *1 s10 *0 r ;※1 ドラムのノイズが消える[ndp.bin v0.47/0.48] 
;                 ;  （NDPBIN_20240902 だと消えない）

3 v13 o2 @6 @r13,4 @q2
3 @l
3 l16
3 s10,1 mk12 @q1 ;※1 @でのH.ENV切替でドラムのノイズが消える場合こちらを使う[ndp.bin v0.47/0.48] 
3 m+ 
; 矩形波のほうが良くうねるが、スラップベース系の原曲と違って不協和音に聞こえやすい
3  [[aaaa]8 [g+g+g+g+]8 [f+f+f+f+]8 [dddd]3 
3   >d<ddd [eeee]2 eeee a>cde<
3  ]2 
3  [[frff>fr<ff]4 [g+rg+g+>g+r<g+g+]4 
3   [eree>er<ee]4 / [araa>a</raa]3 
3                            aaa
3   aa>e<aa>e<aa 
3 ]2 
3  a+ra+ra+ra+r brbrbrbr >crcrcrcr c+rc+rc+c+c+c+<

;********************************
; rhythm 
;********************************

@R:B = *3 :880 N0 V15 / *1 :2000 V14
@R:S = *2 N1 V15 / *3 :520 N0 V14 / *1 :670 V13 / :820 V12 / :970 v11
;@R:S = *3 :520 N1 V15 / *2 :670 N0 V14 / *1 :820 V13
@R:H = *3 :18 N0 V12
@R:O = *3 :20 N0 V12 / V11 / V10
@R:K = *3 :450 N8  V15 / *1 :500 V14 / :550 V13
@R:M = *3 :500 N10 V15 / *1 :630 V14 / :760 V13
@R:N = *3 :720 N12 V15 / *1 :870 V14 / :1020 V13

r v14 vs14
r @l
r l16
r @rc2
r  [[[b8h16h16s8h16h16]3/b8b16s16s8h16h16]4 
r                        b16b16s16b16s16b16s16s16 
r  ]2 
r  [b16h16b16b16s16h16b16b16]14 
r   b16h16s16b16s16s16b16b16s16 
r   b16s16b16s16b16s16s16 
r  [b16h16b16b16s8b16b16]12 
r  [b16h16b16b16s8b16b16 
r   b16h16s16b16s16/s16s16b16]2 
r                   b16s16s16 
