10 CLEAR 200,&H9000:DEFINT A-Z
20 AD=&H9000'LOAD ADR
30 SCREEN 0:WIDTH 40:COLOR 15,4,7:KEYOFF
40 PRINT"[L]:AUTOPLAY"
50 PRINT"LOAD NDP.BIN":GOSUB 350
60 RESTORE
70 'LOAD NDP DATA
80 READ F$:IF F$="*" THEN 60
90 PRINT:F$=F$+".NDP":PRINT"PLAY:"+F$
100 BLOAD F$,AD
110 A=USR2(AD)'SETADR
120 A=USR(0)'PLAY
130 NL=PEEK(AD+11)AND 1 '1=NO LOOP
140 READ T$:PRINT T$
141 IF NL THEN PRINT "(NO LOOP)"
142 CY=CSRLIN:IF CY=23 THEN PRINT:CY=CY-1
150 '
160 'MAIN LOOP
170 I$=INKEY$
175 CC=USR8(LA)+1
176 IF LC THEN LOCATE 0,CY:PRINT"LOOP:";CC;"/";LC-NL
180 IF (I$="L")OR (I$="l")OR STRIG(3)OR STRIG(4)THEN 290
190 TR=STRIG(1)OR STRIG(2)OR(I$<>"")
200 IF NL THEN TR=TR OR(CC>NL)
205 IF LC THEN TR=TR OR(CC>LC)
210 IF TR=0 THEN 170
220 '
230 'PLAY NEXT
240 A=USR7(5)'FADEOUT
250 TIME=0:FOR I=0 TO 5*16:I=TIME:NEXT
260 GOTO 80
270 '
280 'AUTOPLAY MODE
290 LC=2-LC
300 IF LC THEN PRINT"*AUTOPLAY LOOP="+HEX$(LC)
310 IF LC=0 THEN PRINT"*AUTOPLAY OFF"
312 CY=CSRLIN:IF CY=23 THEN PRINT:CY=CY-1
320 I$=INKEY$:IF (I$="L")OR(I$="l")OR STRIG(3)OR STRIG(4)THEN 320
330 GOTO 170
340 '
350 'SET UP
360 BLOAD"NDP.BIN",R
365 DEFUSR9=&HC04B'TERM
370 DEFUSR=&HC003'PLAY
380 DEFUSR1=&HC006'STOP
390 DEFUSR2=&HC00C'SETBGMADR
400 DEFUSR3=&HC00F'MuteA(2-255)
410 DEFUSR4=&HC012'MuteB
420 DEFUSR5=&HC015'MuteC
430 DEFUSR6=&HC018'-VOL(0-15)
440 DEFUSR7=&HC01B'F.O.(1-255)
450 DEFUSR8=&HF975:BLOAD"USRGETA.BIN"'!USE VOICAQ!
460 'USRGETA:A=USR(ADR):CALL(ADR);RET A
470 LA=&HC039'RDLOOP
480 SA=&HC033'RDSTAT
490 LC=2'AUTOPLAY LOOP(0/2)
500 ONSTOPGOSUB 540:STOPON
510 ONERRORGOTO 560
520 RETURN
530 'ON STOP
540 A=USR7(5):TIME=0:FOR I=0 TO 5*16:I=TIME:NEXT:A=USR1(0):A=USR9(0):PRINT"STOP":END
550 'ON ERROR
560 A=USR1(0):A=USR9(0):ONERRORGOTO 0:RESUME
570 '
580 DATA bm2holic,"bm2dx holic psg [by @merge99]"
590 DATA 8bit255,"8bit Sensation 255 [by @merge99]"
600 DATA DGuitar,"Distortion Guitar Sample"
610 DATA FZ-BBlue,"F-ZERO (NDP arrange) BIG BLUE [by peita]"
620 DATA FMiss-1,"Final Mission: Stage1 Newyork City Ruins [by peita]"
630 DATA ZANARKAN,"inalFantasyX: To ZANARKAND [by peita]"
640 DATA ParoGirl,"Parodius: Beautiful Girls V2 [by peita]"
650 DATA Tw3EveGr,"Twinbee3: Evening Grow [by peita]"
660 DATA Sch-COWA,"the SCHEME: COWARAD! [by peita]"
670 DATA *
