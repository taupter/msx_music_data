10 CLEAR 200,&H9000:DEFINT A-Z
20 AD=&H9000 'LOAD ADR
30 SCREEN 0,,0:WIDTH 40:COLOR 15,4,7
40 PRINT"LOAD NDP.BIN":GOSUB 210
50 '
60 RESTORE
70 '
80 READ F$:IF F$="*" THEN 60
90 PRINT:F$=F$+".NDP":PRINT"PLAY:"+F$
100 BLOAD F$,AD
110 A=USR2(AD) 'SET ADR
120 A=USR(0) 'PLAY
130 READ T$:PRINT T$
140 '
150 TR=STRIG(1) OR STRIG(2) OR (INKEY$<>"")
160 IF TR=0 THEN 150
170 A=USR7(5) ' FADEOUT
180 TIME=0:FOR I=0 TO 5*16:I=TIME:NEXT
190 GOTO 80
200 '
210 'SET UP NDP 
220 BLOAD"NDP.BIN"
230 DEFUSR=&HC003  'PLAY
240 DEFUSR1=&HC006 'STOP
250 DEFUSR2=&HC00C 'SET BGM ADR
260 DEFUSR3=&HC00F 'MUTE PSG A(2-255)
270 DEFUSR4=&HC012 'MUTE PSG B
280 DEFUSR5=&HC015 'MUTE PSG C
290 DEFUSR6=&HC018 'VOLUME REDUCE(0-15)
300 DEFUSR7=&HC01B 'FADE OUT(1-255)
310 ON STOP GOSUB 350:STOP ON
320 ON ERROR GOTO 370
330 RETURN
340 ' ON STOP
350 A=USR7(5):TIME=0:FOR I=0 TO 5*16:I=TIME:NEXT:A=USR1(0):PRINT"STOP":END
360 ' ON ERROR
370 A=USR1(0):ON ERROR GOTO 0:RESUME
380 '
390 DATA bm2holic,"bm2dx holic psg [by @merge99]"
400 DATA 8bit255,"8bit Sensation 255 [by @merge99]"
410 DATA DGuitar,"Distortion Guitar Sample"
420 DATA FZ-BBlue,"F-ZERO (NDP arrange) BIG BLUE [by peita]"
430 DATA FMiss-1,"Final Mission: Stage1 Newyork City Ruins [by peita]"
440 DATA ZANARKAN,"inalFantasyX: To ZANARKAND [by peita]"
450 DATA ParoGirl,"Parodius: Beautiful Girls V2 [by peita]"
460 DATA Tw3EveGr,"Twinbee3: Evening Grow [by peita]"
470 DATA Sch-COWA,"the SCHEME: COWARAD! [by peita]"
480 DATA *
