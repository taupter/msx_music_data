10 CLEAR150,&H9000:DEFINT A-Z
20 AD=&H9000'LOAD ADR
30 SCREEN 0:WIDTH 40:COLOR15,4,7:KEYOFF
40 PRINT"[A]:AUTOPLAY"
50 PRINT"LOAD NDP.BIN":GOSUB350
60 RESTORE
70 'LOAD NDP
80 READ F$:IF F$="*"THEN 60
90 PRINT:F$=F$+".NDP":PRINT"PLAY:"+F$
100 BLOAD F$,AD
110 A=USR2(AD)'SETADR
120 A=USR(0)'PLAY
130 NL=PEEK(AD+11)AND 1 '1=NO LOOP
140 READ T$:PRINT T$
141 IF NL THEN PRINT "-NO LOOP"
142 CY=CSRLIN:IF CY=23THEN PRINT:CY=CY-1
150 '
160 'LOOP
170 I$=INKEY$
175 CC=USR8(LA)+1
176 IF LC THEN LOCATE 0,CY:PRINT"LOOP:";CC;"/";LC-NL
180 IF (I$="A")OR (I$="a")OR STRIG(3)OR STRIG(4)THEN 290
190 TR=STRIG(1)OR STRIG(2)OR(I$<>"")
200 IF NL THEN TR=TR OR(CC>NL)
205 IF LC THEN TR=TR OR(CC>LC)
210 IF TR=0THEN 170
220 '
230 'NEXT
240 A=USR7(5)'FO
250 TIME=0:FOR I=0TO 5*16:I=TIME:NEXT
260 GOTO80
270 '
280 'AUTO
290 LC=2-LC
300 IF LC THEN PRINT"AUTOPLAY ON:L="+HEX$(LC)
310 IF LC=0THEN PRINT"AUTOPLAY OFF"
312 CY=CSRLIN:IF CY=23THEN PRINT:CY=CY-1
320 I$=INKEY$:IF(I$="A")OR(I$="a")OR STRIG(3)OR STRIG(4)THEN 320
330 GOTO170
340 '
350 'SETUP
360 BLOAD"NDP.BIN",R
365 DEFUSR9=&HC04B'TERM
370 DEFUSR=&HC003'Play
380 DEFUSR1=&HC006'Stop
390 DEFUSR2=&HC00C'SetAdr
400 DEFUSR3=&HC00F'MuteA(2-255)
410 DEFUSR4=&HC012'MuteB
420 DEFUSR5=&HC015'MuteC
430 DEFUSR6=&HC018'Vol-(0-15)
440 DEFUSR7=&HC01B'FO(1-255)
450 DEFUSR8=&HF975:BLOAD"USRGETA.BIN"'!VOICAQ!
460 'USRGETA:A=USR(A):CALL(A);RET A
470 LA=&HC039'RDLOOP
480 SA=&HC033'RDSTAT
490 LC=2'LOOP(0/2)
500 ONSTOPGOSUB540:STOPON
510 ONERRORGOTO560
520 RETURN
530 'ONSTOP
540 A=USR7(5):TIME=0:FOR I=0TO 5*16:I=TIME:NEXT:A=USR1(0):A=USR9(0):PRINT"STOP":END
550 'ONERROR
560 A=USR1(0):A=USR9(0):ONERRORGOTO0:RESUME
570 '
1000 DATA LR,ØÌÞÙ×ÌÞÙ
1470 DATA *
