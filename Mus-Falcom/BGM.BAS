100 CLEAR 100,&HA5B6:DEFINT A-Z:DIM I(1)
110 BLOAD"bgm.bin"
120 GOSUB 270:V=0:ST=0
130 PRINT"RET:Load BGM data":PRINT"0:Start BGM":PRINT"1:Stop BGM"
140 PRINT"+/- :Volume":PRINT"ESC:End"
150 PRINT"*:Pause":PRINT"/:Continue"
160 A$=INPUT$(1)
170 IF A$=CHR$(13) THEN GOSUB 350:GOSUB 290
180 IF A$="0" AND ST<>0 THEN GOSUB 290
190 IF A$="1" THEN GOSUB 320
200 IF A$="-" AND V<15 THEN V=V+1:GOSUB 340
210 IF A$="+" AND V>0  THEN V=V-1:GOSUB 340
220 IF A$="*" THEN V0=V:V=17:GOSUB 340
230 IF A$="/" THEN V=18:GOSUB 340:V=V0
240 IF A$=CHR$(27) THEN GOSUB 320:END
250 GOTO 160
260 'Initialize
270 DEFUSR=&HCE00:I=USR(0):RETURN
280 'Start BGM
290 I(0)=0:I(1)=ST:DEFUSR=&HCE03:I=USR(VARPTR(I(0))):RETURN
300 DEFUSR=&HCE03:I=USR(VARPTR(I(0))):RETURN
310 'Stop BGM
320 DEFUSR=&HCE06:I=USR(0):RETURN
330 'Master volume
340 I(0)=V:DEFUSR=&HCE09:I=USR(VARPTR(I(0))):RETURN
350 'Load BGM data  &  read start address
360 GOSUB 320:INPUT"File name";A$:A$=A$+".bgm":BLOAD A$
370 OPEN A$ FOR INPUT AS #1:A$=INPUT$(3,#1):CLOSE #1
380 ST=ASC(MID$(A$,2,1)) + ASC(MID$(A$,3,1))*256 - 65536!
390 PRINT "Top address =";HEX$(ST):RETURN
