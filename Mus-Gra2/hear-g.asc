10 '************************************
20 '*    GRADIUS II  MUSIC LIBRARY     *
30 '*      (C)  KONAMI  1988.          *
40 '*    Data Worked by  UNISKIE       *
50 '*     WITH "MuSICA" (C) ASCII      *
60 '************************************
70 KEYOFF:KEY1,"GOSUB210"+CHR$(13):KEY2,"GOSUB200"+CHR$(13):GOTO240
80 PRINT"use SCC GAME Cart(1-2=SLOT / 0=NO)";:A$=INPUT$(1):IFA$="1"ORA$="2"THENPRINTA$:A=VAL(A$):POKE&HD053,&H3E:POKE&HD054,A:POKE&HD055,&H32:POKE&HD056,&H98:POKE&HD057,&HCE:POKE&HD058,0 ELSE IFA$<>"0"THENCLS:GOTO80
90 CLS:RETURN
100 DEFUSR=&HCE00:I=USR(0):RETURN'==INIT==
110 '==ADRESS READ & SET==
120 GOSUB210:GOSUB100:FORI=0TO5:POKE&HA000+I,0:NEXTI
130 F$=F1$:GOSUB190:POKE&HA004,ASC(MID$(AR$,2,1)):POKE&HA005,ASC(MID$(AR$,3,1))
140 'PRINTF$;"=";AD$(0);"-";AD$(1);
150 IFF2$=""THENPOKE&HA002,PEEK(&HA004):POKE&HA003,PEEK(&HA005):RETURN
160 F$=F2$:GOSUB190:POKE&HA002,ASC(MID$(AR$,2,1)):POKE&HA003,ASC(MID$(AR$,3,1))
170 'PRINTF$;"=";AD$(0);"-";AD$(1);
180 RETURN
190 OPEN F$+".BGM" FOR INPUT AS #1:AR$=INPUT$(5,#1):CLOSE#1:FORJ=0TO1:AD$(J)=HEX$(ASC(MID$(AR$,2+J*2,1))+ASC(MID$(AR$,3+J*2,1))*256):NEXT:RETURN' ADDRES CHECK
200 DEFUSR=&HCE03:A=USR(&HA000):POKE&HCE9C,PEEK(&HA004):POKE&HCE9D,PEEK(&HA005):RETURN'==PLAY==
210 DEFUSR=&HCE06:A=USR(0):RETURN'==STOP==
220 I(0)=V:DEFUSR=&HCE09:A=USR(VARPTR(I(0))):RETURN'==VOL==
230 '
240 SCREEN0,,0:WIDTH40:COLOR15,0,0:CLEAR3000,&H9FFF:DEFINTA-Z:DIMI(1):DIMAD$(1):COLOR=(15,4,4,5):COLOR=(0,0,1,1):GOSUB610
250 BLOAD"bgm.bin":MN=21:F1$="gra-4":F2$="":BLOADF1$+".BGM":GOSUB80
260 GOSUB110:V=0:ONSTOPGOSUB560:STOPON:GOSUB580
270 LOCATE0,23:PRINT" [SPC=PLAY_NEXT/RET=SEL/1=STOP/0=PLAY]";
280 A$=INKEY$
290 IFA$=CHR$(24)THENXT=0:GOSUB380
300 IFA$=CHR$(13)THENXT=0:GOSUB380
310 IFA$=CHR$(32)THENXT=1:F=(F MOD22)+1:GOSUB420
320 IFA$="0"THENGOSUB210:GOSUB100:GOSUB200
330 IFA$="1"THENGOSUB210
340 IFA$="-"ANDV<>15THENV=V+1:GOSUB220
350 IFA$="+"ANDV<>0THENV=V-1:GOSUB220
360 GOTO 270
370 '
380 LOCATE0,23:PRINTSPC(39);:LOCATE0,23:PRINT"SELECT NO.=";
390 A$=INKEY$:IF(A$>"2")OR(A$<"0")THENIFA$=CHR$(13)THEN270ELSE390ELSEPRINTA$;
400 B$=INKEY$:IF(B$>"9")OR(B$<"0")THENIFB$=CHR$(13)THEN270ELSE400ELSEPRINTB$;
410 F=VAL(A$+B$):ON-((F>LM)OR(F<1))GOTO380
420 VV=V':V=0:GOSUB220
430 GOSUB210:GOSUB100:IFF>9THENNB$="1"+CHR$(FMOD10+48)ELSENB$=CHR$(FMOD10+48)
440 F2$="":F3$="":F4$="":B$="GRAII-"+NB$:IFF<20THEN460
450 RESTORE670:FORI=20TOF:READF3$,F$:NEXTI:GOTO470
460 F3$=B$:F$=B$:I=INSTR(F$,"-"):IFI>0THENMID$(F$,I,1)="_"
470 ONERRORGOTO540:BLOADF$+".BGM":F4$=F$
480 ONERRORGOTO0
490 ONERRORGOTO520:BLOADF3$+".BGM":MN=F:F1$=F3$:F2$=F4$
500 ONERRORGOTO0:GOSUB580:LOCATE4,MN+0:PRINT">":V=VV:GOSUB220
510 GOSUB110:GOSUB200:RETURN
520 IFXTTHENF=(F MOD22)+1:RESUME430
530 BEEP:IFMN<>FTHENF=MN:RESUME430ELSERESUME500
540 RESUME480
550 '
560 BEEP:SCREEN0,,1:WIDTH80:PRINT"BGM STOP: F1 key(GOSUB210)":PRINT"BGM START: F2 key(GOSUB200)":PRINT:PRINT"END":GOSUB210:END'==END==
570 '
580 I=0:RESTORE:LOCATE5,0:PRINT"< GRADIUS II  MUSIC LIBRARY >"
590 READA$:IFA$=""THEN LM=I:RETURNELSEI=I+1
600 LOCATE1,I+0:PRINTCHR$(I\10+48)+CHR$(IMOD10+48)+": : ";SPC((26-LEN(A$))\2);A$:GOTO590
610 SOUND7,&B10111000:T$="t120l16o5sm9000":PLAYT$,T$,T$
620 PLAY"o4bo5dgbg4","o4dgbo5do4b4","o4g8.o5d16o4g4"
630 FORI=-1TO0:I=PLAY(0):NEXT:RETURN
640 '
650 DATA"Title Demo","Equipment","TABIDACHI","Burning Heat","Take Care!","A Shooting Star","Synthetic Life","Crystal World","A Way Out of The Difficuly"
660 DATA"The Old Stone Age -1-","The Old Stone Age -2-","Maximum Speed","Fire Dragon","Into The Hostile Ship","Shoot and Shoot","The Final Enemy","Farewell","Game Over","Ranking","GRADIUS-2 Stage3","GRADIUS Stage4","DragonSaver Stage1",""
670 '
680 DATA"GRA2-3","","GRA-4","","SUIBOTU","SUIBOTU1"
