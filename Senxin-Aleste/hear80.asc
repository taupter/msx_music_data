10 ' ÖÖÖ BGM PLAYER with MuSICA ÖÖÖ
20 KEY1,"a=usr1(0)"+CHR$(13):KEY6,"a=usr2(&ha500)"+CHR$(13):GOTO280
30 'Ö LOAD DATA Ö
40 VV=V:FORV=VVTO15:GOSUB610:FORI=0TO50:NEXTI,V
50 ON ERROR GOTO 180
60 P$=F$:GOSUB590:BLOAD F$+".BGM"
70 ON ERROR GOTO 0
80 A$=F$:GOSUB210:LA=VAL("&H"+AD$(0)):A0=VAL("&H"+AD$(1))
90 LOCATE 4,22:PRINT"ADDRESS=    &H";AD$(0);">&H";AD$(1);
100 LP=0:IF FF$=SPACE$(8) THEN 130
110 A$=FF$:GOSUB220:IA=VAL("&H"+AD$(0)):A1=VAL("&H"+AD$(1))
120 IF (A0<IA) OR (A1<LA) THEN 140 ELSE BEEP
130 FF$=SPACE$(8):PP$=FF$:PRINT SPC(40):GOTO 160
140 BLOAD FF$+".BGM":LP=1:PP$=FF$:FF$=SPACE$(8)
150 PRINT"      &H";AD$(0);">&H";AD$(1)
160 LOCATE 0,21:PRINT"NOW PLAYING= LOOP ¢"+P$+"£ / INTRO ";:IF PP$=SPACE$(8) THEN PRINT"NOTHING   " ELSE PRINT"¢"+PP$+"£"
170 V=VV:GOSUB610:GOSUB640:I=USR9(0):RETURN
180 'Ö file not found Ö
190 RESUME 200
200 ON ERROR GOTO 0:RETURN
210 'Ö ADDRESS CHECK Ö
220 OPEN A$+".BGM" FOR INPUT AS #1:A$=INPUT$(5,#1):CLOSE#1
230 FORJ=0TO1:AD$(J)=HEX$(ASC(MID$(A$,2+J*2,1))+ASC(MID$(A$,3+J*2,1))*256):NEXT
240 RETURN
250 ' Ö SCC without SOUND CARTRIDGE MODE Ö
260 POKE&HD053,&H3E:POKE&HD054,A:POKE&HD055,&H32:POKE&HD056,&H98:POKE&HD057,&HCE:POKE&HD058,0:RETURN   'A=ADDRESS of GAME CARTRIDGE with SCC
270 'Ö MAIN Ö
280 SCREEN0,,0:WIDTH80:COLOR15,0,0:CLEAR3000,&HA500:KEYOFF:DEFINTA-Z:DIMI(1)
290 DEFUSR=&HCE00:DEFUSR1=&HCE06:DEFUSR2=&HCE03:DEFUSR3=&HCE09:DEFUSR9=&H156
300 COLOR=(15,0,0,0):DEFFNA(I)=VPEEK(X*13+I+(Y+2)*80)
310 PRINT" * BGM PLAYER *  INTRO SET=[RET]/RESET=[BS] / PLAY!=[SPACE]":PRINT
320 BLOAD"BGM.BIN":FILES"*.BGM":PP$=SPACE$(8):FF$=PP$
330 A=1:GOSUB250
340 X=0:Y=0:FORI=0TO7:F$=F$+CHR$(FNA(I)):NEXT
350 GOSUB560:GOSUB50:ONSTOPGOSUB700:STOPON
360 FORI=0TO7:COLOR=(15,I,I,I):FORJ=0TO99:NEXTJ,I
370 A$=INKEY$:ON-(A$="")GOTO440:S=ASC(A$)-28:I=USR9(0)
380 X=X+(S=1)-(S=0):Y=Y+(S=2)-(S=3)
390 IF X<0 THEN X=5:Y=Y-1
400 IF X>5 THEN X=0:Y=Y+1
410 IF Y<0 THEN Y=0
420 IF Y>15 THEN Y=15
430 IF FNA(0)=32 THEN X=X-1:GOTO390
440 F$="":FORI=0TO7:F$=F$+CHR$(FNA(I)):NEXT
450 LOCATE 4,19:PRINT"SELECT : LOOP ¢"+F$+"£ / INTRO ";:IF FF$=SPACE$(8) THEN PRINT"NOTHING   " ELSE PRINT "¢"+FF$+"£"
460 LOCATE X*13,Y+2:PRINT"ÖSELECTÖ"
470 IFA$="0"THENGOSUB650
480 IFA$="1"THENGOSUB590
490 IFA$="-"ANDV<>15THENV=V+1:GOSUB610
500 IFA$="+"ANDV<>0THENV=V-1:GOSUB610
510 IFA$=" "THEN GOSUB30
520 IFA$=CHR$(13) THEN FF$=F$
530 IFA$=CHR$(8) THEN FF$=SPACE$(8)
540 LOCATE X*13,Y+2:PRINT F$
550 GOTO370
560 'Ö FIRST SETTING Ö
570 I=USR(0)
580 RETURN
590 'Ö BGM STOP Ö
600 I=USR1(0):RETURN
610 'Ö VOLUME CHANGE Ö
620 I=USR3(VARPTR(V))
630 RETURN
640 'Ö PLAY BGM Ö
650 POKE&HA500,0:IF LP=1 THEN MA=IA ELSE MA=LA
660 POKE&HA502,MA AND255:POKE&HA503,VAL("&h"+MID$(HEX$(MA),1,2))
670 A=USR2(&HA500):POKE&HCE9C,(LA AND255):POKE&HCE9D,VAL("&h"+MID$(HEX$(LA),1,2))
680 RETURN
690 'Ö END Å
700 SCREEN0:LOCATE0,1:PRINT"a=usr1(0) 'STOP":KEYON:LOCATE0,0
